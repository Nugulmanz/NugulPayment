plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.google.protobuf' version '0.9.2'
}

group = 'com.sparta'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs += file('grpc-proto/build/generated/source/proto/main/java')
            srcDirs += file('grpc-proto/build/generated/source/proto/main/grpc')
        }
    }
}


protobuf {
    protoc { artifact = "com.google.protobuf:protoc:4.28.3" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.68.1" }
    }
    generateProtoTasks {
        all().forEach { task ->
            task.builtins {
                java {}
            }
            task.plugins {
                grpc {}
            }
        }
    }
}


dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-core:2.15.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.15.0'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // AWS
    implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:3.0.1")
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-sqs'
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-sns'

    // gRPC
    implementation "io.grpc:grpc-netty-shaded:1.68.1"
    implementation "io.grpc:grpc-protobuf:1.68.1"
    implementation "io.grpc:grpc-stub:1.68.1"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'net.devh:grpc-spring-boot-starter:3.1.0.RELEASE'

}

// 빌드 전에 grpc-proto의 Java 파일이 생성
tasks.named('compileJava') {
    dependsOn gradle.includedBuild('grpc-proto').task(':generateProto')
}


tasks.named('test') {
    useJUnitPlatform()
}
